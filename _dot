#compdef _dot dot

DOTFILES_HOME=${DOTFILES_HOME:-~/.config/dotfiles}

function __get_presets {
	find ${DOT_PRESETS_HOME:-$DOTFILES_HOME/presets/*} -regex '.*.preset' \
		-maxdepth 1 -mindepth 1 |
		sed -e 's|.*/||' -e 's/.preset//' -e 's/^/+/' -e 's/$/ /'
}

function __get_tags {
	find ${DOT_MODULES_HOME:-$DOTFILES_HOME/modules/*} \
		-maxdepth 1 -mindepth 1 -name '.tags' \
		-exec cat {} + | grep "^[^#;]" |
		sort | uniq | sed -e 's/^/:/' -e 's/$/ /'
}

function _dot_modules {
	_path_files -S " " -r " " -/ -W ${DOT_MODULES_HOME:-$DOTFILES_HOME/modules}
}

function _dot_presets {
	local presets=("${(@f)$(__get_presets)}")
	_multi_parts " " 'presets'
}

function _dot_tags {
	local tags=("${(@f)$(__get_tags)}")
	_multi_parts " " 'tags'
}

function _dot_all {
	_dot_modules
	_dot_presets
	_dot_tags
}

function _dot {
    _arguments -S -C \
       '(-h --help)'{-h,--help}'[output usage information]' \
       '(-f --force)'{-f,--force}'[force execution]'\
       "*:: :{ _dot_all }"
}

